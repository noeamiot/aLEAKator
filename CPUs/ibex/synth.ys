# Perform a bottom up "synthesis", one for the ibex core then one for the
# simple_ibex_system implementing it.
# Most of the commands here are the one executed with the synth comnand
# the main difference being, we never call: wreduce, alumacc, abc and
# only conditionnaly memory_map.
echo on
read_verilog -defer ./ibex_gen_verilog/*.v
read_verilog -defer -sv ./ibex_gen_verilog/ibex_register_file_ff.sv

# Concretize parameters for our top
hierarchy -check -top $abstract\ibex_simple_system

# First "synthesis", select the ibex as our top
select -module ibex_top
flatten;
proc;
pmuxtree;
opt_expr;
opt_clean;;;

check;
opt -nodffe -nosdff;
fsm;
opt;
peepopt;
opt_clean;
share;
opt;
memory -nomap;
opt_clean;

opt -fast -full;
memory_map;
opt -full;
opt_clean;
dffunmap;

# We want to keep everything that has been produced here
setattr -set keep 1 w:*;
select -clear;
stat;

# Second "synthesis", select our real top as top
hierarchy -check -top ibex_simple_system;

flatten;
proc;
pmuxtree;

# These signals must be split for cxxrtl to not identify false logic loops
select -module ibex_simple_system u_top.u_ibex_core.id_stage_i.controller_i.ctrl_fsm_cs u_top.u_ibex_core.cs_registers_i.pc_wb_i u_top.u_ibex_core.if_stage_i.gen_prefetch_buffer.prefetch_buffer_i.fetch_addr_q u_top.u_ibex_core.ex_block_i.genblk3.gen_multdiv_fast.multdiv_i.imd_val_q_i u_top.u_ibex_core.cs_registers_i.mcountinhibit_q u_top.u_ibex_core.cs_registers_i.dcsr_q u_top.u_ibex_core.cs_registers_i.csr_mtvec_o u_top.u_ibex_core.cs_registers_i.csr_depc_o u_top.u_ibex_core.cs_registers_i.cpuctrlsts_part_q u_top.crash_dump_o
splitnets -format @@;
select -clear;

rename ibex_simple_system top;

opt_expr;
opt_clean;;;

check;
opt -nodffe -nosdff;
fsm;
opt;
peepopt;
opt_clean;
share;
opt;
memory -nomap;
opt_clean;


opt -full;
opt_clean -purge;

# Transform dff to basic types
dffunmap;

# Transform all wires to public wires
rename -enumerate;

# Write our final cxxrtl model
write_cxxrtl -nohierarchy -noflatten -noproc -O6 -g4 -print-symbolic -header ./cxxrtl_ibex.cpp;
write_table ibex.tsv

stat;
